include(TemplateProject)

set(APP_INCLUDE_DIR ${CMAKE_SOURCE_DIR}/include)
file(GLOB_RECURSE APP_SOURCE_FILES ${CMAKE_SOURCE_DIR}/src/*.cpp)
file(GLOB_RECURSE APP_TEST_SOURCES ${CMAKE_SOURCE_DIR}/test/*.cpp)

# Macros
macro(app_define_binary_target)
    set(APP_BINARY_TARGET ${CMAKE_PROJECT_NAME})
    # Application
    add_executable(${CMAKE_PROJECT_NAME} ${APP_SOURCE_FILES})
    target_include_directories(${CMAKE_PROJECT_NAME} PUBLIC ${APP_INCLUDE_DIR})

    if (PLATFORM_WINDOWS)
        # Make sure to link all relevant Win32 API libs, since a lot of external deps need those
        target_link_libraries(${CMAKE_PROJECT_NAME} kernel32 user32 ws2_32 Onecore)
    endif ()
endmacro()

macro(app_define_static_target)
    set(APP_STATIC_TARGET "${CMAKE_PROJECT_NAME}_static")
    # Static library (for tests)
    add_library("${CMAKE_PROJECT_NAME}_static" STATIC ${APP_SOURCE_FILES})
    target_include_directories("${CMAKE_PROJECT_NAME}_static" PUBLIC ${APP_INCLUDE_DIR})

    if (PLATFORM_WINDOWS)
        target_link_libraries("${CMAKE_PROJECT_NAME}_static" kernel32 user32 ws2_32 Onecore)
    endif ()
endmacro()

macro(app_define_test_target)
    set(APP_TEST_TARGET "${CMAKE_PROJECT_NAME}_test")
    # Tests
    add_executable("${CMAKE_PROJECT_NAME}_test" ${APP_TEST_SOURCES})
    target_include_gtest("${CMAKE_PROJECT_NAME}_test")
    target_include_directories("${CMAKE_PROJECT_NAME}_test" PUBLIC ${APP_INCLUDE_DIR})
    target_link_libraries("${CMAKE_PROJECT_NAME}_test" "${CMAKE_PROJECT_NAME}_static")
    add_dependencies("${CMAKE_PROJECT_NAME}_test" "${CMAKE_PROJECT_NAME}_static")
endmacro()

macro(app_define_targets)
    app_define_binary_target()
    app_define_static_target()
    app_define_test_target()
endmacro()

macro(app_maven_dependency url group name version)
    if(DEFINED APP_BINARY_TARGET)
        target_maven_dependency(${APP_BINARY_TARGET} ${url} ${group} ${name} ${version})
    endif()
    if(DEFINED APP_STATIC_TARGET)
        target_maven_dependency(${APP_STATIC_TARGET} ${url} ${group} ${name} ${version})
    endif()
    if(DEFINED APP_TEST_TARGET)
        target_maven_dependency(${APP_TEST_TARGET} ${url} ${group} ${name} ${version})
    endif()
endmacro()

macro(app_include_directories)
    if(DEFINED APP_BINARY_TARGET)
        target_include_directories(${APP_BINARY_TARGET} ${ARGN})
    endif()
    if(DEFINED APP_STATIC_TARGET)
        target_include_directories(${APP_STATIC_TARGET} ${ARGN})
    endif()
    if(DEFINED APP_TEST_TARGET)
        target_include_directories(${APP_TEST_TARGET} ${ARGN})
    endif()
endmacro()

macro(app_link_libraries)
    if(DEFINED APP_BINARY_TARGET)
        target_link_libraries(${APP_BINARY_TARGET} ${ARGN})
    endif()
    if(DEFINED APP_STATIC_TARGET)
        target_link_libraries(${APP_STATIC_TARGET} ${ARGN})
    endif()
    if(DEFINED APP_TEST_TARGET)
        target_link_libraries(${APP_TEST_TARGET} ${ARGN})
    endif()
endmacro()